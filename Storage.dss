// -----------------------------------------------------------------------//
// Autores: Paulo Radatz e Celso Rocha
// e-mails: opendss.brasil@gmail.com, paulo.radatz@gmail.com e celsorocha11@gmail.com 
// -----------------------------------------------------------------------//

// Reinicia o Programa
Clear

// Dados de Fronteira
New Circuit.Equivalente bus1=A pu=1.02 basekv=138 
~ Z0=[0.025862916, 0.077588748] Z1=[0.023094242, 0.092376969]

// Dados do Transformador
New Transformer.Trafo phases=3 windings=2 %loadloss=0.15 xhl=5 %noloadloss=0.015 %imag=2 
~ wdg=1 bus=A kv=138 kva=3000 conn=delta
~ wdg=2 bus=B kv=13.8 kva=3000 conn=wye
~ sub=Yes subname=Subestacao

// Dados dos Arranjos
New Linecode.MeuArranjo4 nphases=4 basefreq=60 units=km
~ rmatrix = [0.249 |0.059 0.249 |0.059 0.059 0.249 |0.059 0.059 0.059 0.427]       !ohm/km
~ xmatrix = [0.878 |0.529 0.878 |0.451 0.484 0.878 |0.467 0.488 0.476 0.960]   	   !ohm/km
~ cmatrix = [9.353 |-3.028 9.858 |-1.160 -1.928 8.891 |-1.393 -1.772 -1.782 8.809] !nF/km
~ neutral=4 kron=No

New Linecode.MeuArranjo3 nphases=3 basefreq=60 units=km
~ rmatrix = [0.249 |0.059 0.249 |0.059 0.059 0.249]     !ohm/km
~ xmatrix = [0.878 |0.488 0.878 |0.488 0.488 0.878]     !ohm/km
~ cmatrix = [8.932 |-2.290 8.932 |-2.290 -2.290 8.932]  !nF/km

// Dados dos Trechos
New Line.LinhaBC bus1=B.1.2.3.0 bus2=C.1.2.3.4 length=2.8 units=km linecode=MeuArranjo4
New Line.LinhaCD bus1=C.1.2.3 bus2=D.1.2.3 length=2.6 units=km linecode=MeuArranjo3

// Dados das Curvas de Carga
New Loadshape.industrial npts=24 interval=1 
~ mult=(0.1 0.1 0.2 0.3 0.3 0.4 0.6 1.2 1.8 1.8 1.9 1.9 1.4 1.6 1.8 1.7 1.7 1.1 0.8 0.6 0.5 0.4 0.2 0.2)  
New Loadshape.residencial npts=24 interval=1 
~ mult=(0.6 0.5 0.4 0.4 0.5 0.8 1.0 0.8 0.8 0.9 1.0 1.0 1.1 0.9 0.9 0.9 1.0 1.2 1.5 1.5 1.7 1.5 1.2 0.8)

// Dados das Cargas
// Model=1 -> Potencia Constante
New Load.CargaC phases=1 bus1=C.1.4 kv=7.9674 kw=500 pf=0.92 model=1 daily=residencial
New Load.CargaD phases=3 bus1=D conn=wye kv=13.8 kw=500 pf=0.92 model=1 daily=industrial

// Curva de despacho do elemento Storage
New LoadShape.despacho_storage npts=24 interval=1 
~ mult=[0 -0.3 -0.3 -0.3 0 0 0 0 0 0 0 0 -0.5 -0.5 0 0 0.35 0.35 1 0.3 0 0 0 0]

// Storage 1
New Storage.storage1 phases=3 bus1=storage_D kv=0.38 pf=1 kWrated=1600 %reserve=20
~ kWhrated = 3200 %stored=30 state=idling dispmode=follow model=1 daily=despacho_storage

// Trafo Interconex√£o Storage 1  
New Transformer.Trafo_storage_D phases = 3 windings=2 xhl=5 %noloadloss=0.39 %loadloss=1.47  
~ wdg =1  bus=D  kV=13.8 kVA=2000 conn=wye 
~ wdg =2  bus=storage_D  kV=0.38 kVA=2000 conn=wye

// Medidor
New EnergyMeter.MedidorSub element=Transformer.Trafo terminal=1

// Monitores
// Monitores na Sub (deve ser conectado em um elemento e nao em uma barra)
New Monitor.PotenciaSub element=Transformer.Trafo terminal=1 mode=1 ppolar=no
New Monitor.TensaoSub element=Transformer.Trafo terminal=1 mode=0

// Monitores na CargaD 
New Monitor.PotenciaCargaD element=Load.CargaD terminal=1 mode=1 ppolar=no
New Monitor.TensaoCargaD element=Load.CargaD terminal=1 mode=0  

//Monitor Storage
New monitor.Mon_Storage1_State element=Storage.storage1 terminal=1 mode=3

// Definindo Tensoes de base
Set voltagebases=[138 13.8 0.38]
Calcvoltagebases
  
// Time-Series Mode
Set controlmode=Static
Set mode=Daily
Set stepsize=1h
Set number=24
Solve

// Coordenadas
BusCoords Coordenadas.csv

// Resultados do Medidor
// Show meters
// Export meters

// ==========================
// Resultados dos Monitores
// ==========================

// Monitor de Potencia da Sub
// Export monitors potenciasub
// Plot monitor object= potenciasub channels=(1 3 5 )

// // Monitor de Tensao da Sub
// Export monitors tensaosub
// Plot monitor object= tensaosub channels=(1 3 5 ) bases=[79674.3 79674.3 79674.3]

// // Monitor de Potencia da CargaD
// Export monitors potenciacargad
// Plot monitor object= potenciacargad channels=(1 3 5 )

// // Monitor de Tensao da CargaD
// Export monitors tensaocargad
// Plot monitor object= tensaocargad channels=(1 3 5 ) bases=[7967.4 7967.4 7967.4]

// // Monitor do StorageD
// Export monitors Mon_Storage1_State
// Plot monitor object= potenciacargad channels=(1 2 3 4 5 6 7 8)
